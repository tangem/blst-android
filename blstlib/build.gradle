plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.tangem.blstlib'
    compileSdk 33
    ndkVersion '24.0.8215888'

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = [
                    'src/main/jniLibs',
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

tasks.register('runNdkBuild', Exec) {
    def ndkDir = android.ndkDirectory
    def blstlibDir = project.projectDir.path
    executable = "$ndkDir/ndk-build"
    args = ["APP_BUILD_SCRIPT=$blstlibDir/src/main/cpp/Android.mk",
            "NDK_PROJECT_PATH=$blstlibDir",
            "NDK_LIBS_OUT=src/main/jniLibs",
            "APP_PLATFORM=23",
            "NDK_APPLICATION_MK=$blstlibDir/src/main/cpp/Application.mk"]
}

preBuild.dependsOn runNdkBuild

apply from: 'maven-push.gradle'
